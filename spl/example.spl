/*
 *  STFL - The Structured Terminal Forms Language/Library
 *  Copyright (C) 2006  Clifford Wolf <clifford@clifford.at>
 *
 *  This program is free software; you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation; either version 2 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program; if not, write to the Free Software
 *  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 *
 *  example.spl: A little STFL SPL example
 */

load "sql";
load "stfl";

var db = sql_connect("sqlite", "example.db");

function search()
{
	var f = stfl_create(<:>
		: table
		:   @input.style:attr="underline"
		:   label
		:     .expand:0
		:     .border:lt
		:     text:"Name:"
		:   input[in_name]
		:     .expand:h
		:     .border:rt
		:     .colspan:2
		:     text[in_name_val]:""
		:   tablebr
		:   label
		:     .expand:0
		:     .border:lb
		:     text:"E-Mail:"
		:   input[in_email]
		:     .expand:h
		:     .border:b
		:     text[in_email_val]:""
		:   label
		:     .expand:0
		:     .border:lrtb
		:     text:"Press ENTER to search"
		:   tablebr
		:   list[result]
		:     .tie:lrt
		:     .colspan:3
		:     .border:lrtb
	</>);

	while (1)
	{
		switch
		{
			var event = stfl_run(f, 0);

		case event ~== "ENTER" and stfl_get_focus(f) =~ /^in_/:
			var in_name = stfl_get(f, "in_name_val");
			var in_email = stfl_get(f, "in_email_val");
			var tuples = sql(db, <:>
				: SELECT * FROM people WHERE 1
				<spl:if code="in_name ~!= ''">
				:	AND name LIKE ${sql::"%$in_name%"}
				</spl:if>
				<spl:if code="in_email ~!= ''">
				:	AND email LIKE ${sql::"%$in_email%"}
				</spl:if>
				: LIMIT 50
			</>);
			stfl_import(f, "result", "replace_inner", <:>
				: list
				<spl:foreach var="[]t" list="tuples">
				:   listitem
				:       text:${stfl::t['name']}"  <"${stfl::t['email']}">"
				</spl:foreach>
			</>);
			
		case event ~== "ESC":
			stfl_return();
			return;
		}
	}
}

search();

